#Область ПрограммныйИнтерфейс
//
Процедура ДополнитьФорму(Форма)Экспорт
    
    //{{ Кривошеев
    ИмяФормы = Форма.ИмяФормы;
    
    Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
        ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
        ДобавитьЭлементыСкидкиЗаказПокупателя(Форма);
    ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
        ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
    ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
        ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
    ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
        ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
    ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
        ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
    КонецЕсли;
    //Кривошеев }}
    
КонецПроцедуры

#КонецОбласти
#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт
    
    //{{ Кривошеев
    ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
    ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
    ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
    //Кривошеев }}
    
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) Экспорт
    
	//{{ Кривошеев
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	//Кривошеев }}
    
КонецПроцедуры

Процедура ДобавитьЭлементыСкидкиЗаказПокупателя(Форма) Экспорт
    
    //{{ Кривошеев
    ЭлементОрганизация = Форма.Элементы.Найти("Организация");
    
    Если ЭлементОрганизация = Неопределено Тогда
          Возврат;
    КонецЕсли;
    
    
    НоваяГруппа = Форма.Элементы.Вставить("ГруппаСогласованнаяСкидка", Тип("ГруппаФормы"),ЭлементОрганизация.Родитель, );
    НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
    НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
    НоваяГруппа.ОтображатьЗаголовок = Ложь;
    
    НовоеПоле = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), НоваяГруппа);
    НовоеПоле.Вид = ВидПоляФормы.ПолеВвода;
    НовоеПоле.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
    НовоеПоле.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");    
    
    НоваяКоманда = Форма.Команды.Добавить("ПересчитатьТаблицу");
    НоваяКоманда.Заголовок = "Пересчитать"; 
    НоваяКоманда.Действие =  "ПересчитатьТаблицу";     
    
    НоваяКнопка = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"),НоваяГруппа); 
    НоваяКнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
    НоваяКнопка.ИмяКоманды = "ПересчитатьТаблицу";
    //Кривошеев }}
	
   КонецПроцедуры

#КонецОбласти